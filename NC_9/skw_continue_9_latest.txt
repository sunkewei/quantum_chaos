/*  **************************************
 * Nuclear Version 8.0
 @ Dependency
 skw_formal_print_array
 skw_formal_array_functions
 skw_steps_calc
 
 ******************************************/

Params 
	Numeric slots(1);
    Numeric step_base(3);

Vars
	Numeric latestoperation(4);
	
	Numeric step;
	Numeric step_value;
	Numeric step_factor;
	String step_sign;
	Numeric bias_value;
	
	Numeric chiduo;
	Numeric chikong;
	
	
	Numeric keping;
	Numeric remain;
	
	Numeric currentAsk;
	Numeric currentBid;
	
	Numeric buyprice; 
	Numeric sellprice;
	Numeric origin_restore_price;
	Numeric max_round;
	Numeric nudge_index;
	Numeric occupied_flag;
	Numeric rand_num;
	
	
	Numeric opencount;
	Numeric totalposition(0);
	Numeric i;
	Numeric nBorS;
	Numeric nEorE;
	
	
	Numeric orderprice;
	String ordertype;
	String ordercontractno;
	
	NumericArray openaccountbuy[100];
	NumericArray openaccountsell[100];
	
	Bool priceinbuy;
	Bool priceinsell;
	Bool balance_priceinsell;
	Bool balance_priceinbuy; 
	
	
	String filestringbuy; 
	String filestringsell; 
	String outputfilename;
	String outputtradefilename;
	
	
	
	
	Numeric duokongminus(0);
	Numeric duokongminus_param(0);

	
	Numeric timeholder;
	String pinzhong;
	
	Numeric totalnumber;
	Numeric totalindex;
	String restore_type;
	Numeric restore_price;
	Numeric tmp_restore_price;
	String totalsign;
	Bool realbuy;
	
	
	Numeric today_lowerlimit;
	Numeric	today_upperlimit;
	Numeric outer_count;
	Bool outer_flag;
	
	String output_str;
	Numeric temp_array_size;
	NumericArray test;
	Numeric sell_array_max;
	Numeric avoid_duplicated_restore_price;
	
	Numeric plot_value;
	Numeric tailered_sell_size;
	Numeric tailered_buy_size;
	String conflict_str;
	
	Numeric accu_buy;
	Numeric accu_sell;
	Numeric should_sell_count;
	Numeric should_buy_count;
	
	
	  
Begin
	
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	// Initialization 
	///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	////////////////////////////////
	// Initialize Variables
	//////////////////////////////// 
	realbuy = True;
	pinzhong = Symbol();
	outputfilename = "c:\\continue_"+pinzhong+".txt";
	outputtradefilename = "c:\\trade_"+pinzhong+".txt";
	step_sign = pinzhong+"_steps";
	totalsign = pinzhong+"_total";
	
	step_value = Value(GetTBProfileString(step_sign,"step"));
	step_factor = Round(step_value/step_base, 0);
	step_value = Round(step_factor, 0)*step_base;
	step = Max(step_base, step_value);
	
	bias_value = Value(GetTBProfileString(step_sign,"bias"));
	timeholder = CurrentTime();
	
	plot_value = Average(low, 20);
	If(bias_value==1){
		PlotNumeric("bias",plot_value , 0, green, 0);
	}Else If(bias_value==-1){
		PlotNumeric("bias",plot_value, 0, red, 0);
	}Else{
		PlotNumeric("bias",plot_value, 0, yellow, 0);
	}
	
	
	If( barstatus==0 ){
		SetGlobalVar( latestoperation, 0 );
	}
	
	////////////////////////////////
	// Trade Protection
	////////////////////////////////
	FileAppend(outputfilename,"======================"+Text(CurrentTime)+"=============================");
	If (Weekday==6 || Weekday==0){
		FileAppend(outputfilename,"Weekend, no trading");
	}	
	If(timeholder>=0.150100 && timeholder<=0.205000){
		return;
	}
	If(timeholder>=0.000000 && timeholder<=0.085500){
		return;
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	// Trading
	///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	///////////////////////////////////
	// Judge whether Account is ready
	///////////////////////////////////
	if(BarStatus==2 && AccountDataExist){
		
		today_lowerlimit = Q_LowerLimit();
		today_upperlimit = Q_UpperLimit(); 
		currentAsk = Q_AskPrice();
		currentBid = Q_BidPrice();
		
		If(!(today_lowerlimit > 0 && today_upperlimit > 0 && currentAsk > 0 && currentBid > 0 ) ){
			FileAppend(outputfilename,"----------account not on, no trading----------");
			return;
		}
		If(currentAsk==today_upperlimit || currentBid== today_lowerlimit){
			FileAppend(outputfilename,"----------price limit hit, no trading----------");
			return;
		}
	}
	
	
	
	
	///////////////////////////////
	// End of the Day 
	///////////////////////////////
	
	if(BarStatus==2 && AccountDataExist){
		If((timeholder>=0.145950 && timeholder<=0.151000)){
			If(Value(GetTBProfileString(totalsign,"stored")) == 1){
				FileAppend(outputfilename,"----------already stored----------");
				return; 
			}
			FileAppend(outputfilename,"----------Entering end of the day----------");
			opencount = A_GetOpenOrderCount();
			outer_count = Value(GetTBProfileString(totalsign,"total"));
			SetTBProfileString(totalsign,"total", Text(opencount+outer_count));
			For i = outer_count+1 To outer_count+opencount
			{
				nBorS = A_OpenOrderBuyOrSell(i-outer_count);
				nEorE = A_OpenOrderEntryOrExit(i-outer_count);
				orderprice = A_OpenOrderPrice(i-outer_count);
				ordercontractno = A_OpenOrderContractNo(i-outer_count);
				ordertype = "";
				If (nBorS == Enum_Buy()){
					ordertype = "b";
				}Else If(nBorS==Enum_Sell()){
					ordertype = "s";
				}
				SetTBProfileString(pinzhong+"_"+Text(i),"type", ordertype);
				SetTBProfileString(pinzhong+"_"+Text(i),"price", Text(orderprice));
				A_DeleteOrder(ordercontractno);
			}
			SetTBProfileString(totalsign,"stored", Text(1));
			return;
		}Else{
			SetTBProfileString(totalsign,"stored", Text(0));
		}
	
	}
	

	/////////////////////////////////////////////
	// Perform Trading Actions
	////////////////////////////////////////////
	if(BarStatus==2 && AccountDataExist){
	
		currentAsk = Q_AskPrice();
		currentBid = Q_BidPrice();
		chiduo=A_BuyPosition(); 
		chikong=A_SellPosition();
		
		totalnumber = Value(GetTBProfileString(totalsign,"total"));
		NumericArrayClear(openaccountbuy);
		NumericArrayClear(openaccountsell);
		
		For i = 1 To totalnumber{
			restore_type = GetTBProfileString(pinzhong + "_" + Text(i),"type");
			restore_price = Value(GetTBProfileString(pinzhong + "_" + Text(i),"price"));
		
			If(Exact(restore_type,"b")==True){
				NumericArrayInsert(openaccountbuy, -1, restore_price);
			}
			If(Exact(restore_type,"s")==True){
				NumericArrayInsert(openaccountsell, -1, restore_price);
			}
		}
		NumericArraySort(openaccountbuy, False);
		NumericArraySort(openaccountsell, True);
		
		currentAsk = Q_AskPrice();
		currentBid = Q_BidPrice();
		
		tailered_buy_size = GetNumericArraySize(openaccountbuy);
		tailered_sell_size =  GetNumericArraySize(openaccountsell);
		
		outer_count = 0;
		For i = 1 To tailered_buy_size{
			tmp_restore_price = openaccountbuy[i-1];
			if(i<=6 || tmp_restore_price >= currentBid - 3*step){
				outer_count = outer_count +1;
				SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "b");
				SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(openaccountbuy[i-1])); 
			}
		}
		For i = 1 To tailered_sell_size{
			tmp_restore_price = openaccountsell[i-1];
			if(i<=6 || tmp_restore_price <= currentBid + 3*step){
				outer_count = outer_count +1;
				SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "s");
				SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(openaccountsell[i-1])); 
			}
		}
		SetTBProfileString(totalsign,"total", Text(outer_count));
		totalnumber = outer_count;
		
		totalnumber = Value(GetTBProfileString(totalsign,"total"));
		
		skw_formal_print_array(outputfilename, "sell forbid", openaccountsell);
		skw_formal_print_array(outputfilename, "buy forbid", openaccountbuy);
		
		
		
		
		
		
		

		
		chiduo=A_BuyPosition(); 
		chikong=A_SellPosition();
		//step_value = Value(GetTBProfileString(step_sign,"step"));
		//step_value = Round(step_value/step_base, 0)*step_base;
		//step = Max(step_base, step_value) ;
		FileAppend(outputfilename,"step: "+ Text(step) + "\tbias: "+ Text(bias_value));
		
		If(totalnumber>0){
			outer_count = 0;
			for  totalindex = 1 To totalnumber {
				outer_flag = False;
				restore_type = GetTBProfileString(pinzhong + "_" + Text(totalindex),"type");
				restore_price = Value(GetTBProfileString(pinzhong + "_" + Text(totalindex),"price"));
				
				If(Exact(restore_type,"b")==True){
					// perform real buy action
					
					If( restore_price <= currentBid ){
						outer_flag = True;
					}Else{
						If(restore_price%step!=0){
							outer_flag = False;
						}
						/*
						Else If(bias_value == -1){
							
							outer_flag = False;
							FileAppend(outputfilename,"bais is -1, cancelled "+Text(restore_price));
							accu_buy = Value(GetTBProfileString(step_sign,"accu_buy"));
							accu_buy = accu_buy +1;
							SetTBProfileString(step_sign,"accu_buy", Text(accu_buy));
							
							
							// nudge and change restore price to lower available place
							//tmp_restore_price = Floor(currentBid, step_base);
							//while (tmp_restore_price > 0){
							//	occupied_flag = skw_formal_array_functions("contains", openaccountbuy, tmp_restore_price);
							//	if (occupied_flag == 0){
							//		FileAppend(outputfilename,"bais is -1, nudged the current price from "+Text(restore_price)+" to "+Text(tmp_restore_price));
							//		restore_price = tmp_restore_price;
							//		Break;
							//	}
							//	tmp_restore_price = tmp_restore_price - step_base;
								
							//}
							//If(tmp_restore_price > 0){
							//	outer_flag = True;
							//}
							
						}*/
						Else{
							avoid_duplicated_restore_price = restore_price - step;
							occupied_flag = skw_formal_array_functions("contains", openaccountbuy, avoid_duplicated_restore_price);
							If(occupied_flag == 0 && bias_value==0){
								FileAppend(outputfilename,"no need to buy and instantly sell, kick  "+Text(restore_price)+" to "+Text(avoid_duplicated_restore_price));
								restore_price = avoid_duplicated_restore_price;
								outer_flag = True;
							}Else{
								chiduo=A_BuyPosition(); 
								chikong=A_SellPosition();
						
								If(Abs(chikong)==0 && CurrentBar > GetGlobalVar(latestoperation) ){
									FileAppend(outputfilename,"perform trade: ++ kaiduo\t"+Text(slots)+"\t"+Text(restore_price));
									FileAppend(outputtradefilename, Text(CurrentDate)+"-"+Text(CurrentTime)+"\t++ kaiduo\t"+Text(slots)+"\t"+Text(currentBid));
									If(realbuy==True){
										A_SendOrder(Enum_Buy,Enum_Entry,slots,restore_price);
										SetGlobalVar(latestoperation, CurrentBar);
									}
								}Else If(Abs(chikong)>0 && CurrentBar > GetGlobalVar(latestoperation)  ){
									FileAppend(outputfilename,"perform trade: ++ pingkong\t"+Text(slots)+"\t"+Text(restore_price));
									FileAppend(outputtradefilename, Text(CurrentDate)+"-"+Text(CurrentTime)+"\t++ pingkong\t"+Text(slots)+"\t"+Text(currentBid));
									If(realbuy==True){
										If(A_TodaySellPosition==0){
											A_SendOrder(Enum_Buy,Enum_Exit,slots, restore_price);
										}Else{
											A_SendOrder(Enum_Buy,Enum_ExitToday,slots, restore_price);
										}
										SetGlobalVar(latestoperation, CurrentBar);
									}
								}Else{
									outer_flag = True;
								}
							}
						}
					}
				}

				
				If(Exact(restore_type,"s")==True){
					
					
					If( restore_price >= currentAsk ){
						outer_flag = True;
					}Else{
						// nudge and change restore price to higher available place
						If(restore_price%step!=0){
							outer_flag = False;
						}
						/*
						Else If(bias_value == 1){
						
							outer_flag = False;
							FileAppend(outputfilename,"bais is 1, cancelled "+Text(restore_price));
							accu_sell = Value(GetTBProfileString(step_sign,"accu_sell"));
							accu_sell = accu_sell +1;
							SetTBProfileString(step_sign,"accu_sell", Text(accu_sell));
							
							//tmp_restore_price = Ceiling(currentAsk,step_base);
							//sell_array_max = skw_formal_array_functions("max",openaccountsell, 0);
							//while (tmp_restore_price <= sell_array_max + 10*step_base){
							//	occupied_flag = skw_formal_array_functions("contains", openaccountsell, tmp_restore_price);
							//	if (occupied_flag == 0){
							//		FileAppend(outputfilename,"bais is 1, nudged the current price from "+Text(restore_price)+" to "+Text(tmp_restore_price));
							//		restore_price = tmp_restore_price;
							//		Break;
							//	}
							//	tmp_restore_price = tmp_restore_price + step_base;
							//	
							//}
							//If(tmp_restore_price > 0){
							//	outer_flag = True;
							//}
							
						
						}*/
						Else{
							avoid_duplicated_restore_price = restore_price + step;
							occupied_flag = skw_formal_array_functions("contains", openaccountsell, avoid_duplicated_restore_price);
							If(occupied_flag == 0  && bias_value==0){
								FileAppend(outputfilename,"no need to sell and instantly buy, kick  "+Text(restore_price)+" to "+Text(avoid_duplicated_restore_price));
								restore_price = avoid_duplicated_restore_price;
								outer_flag = True;
							}Else{
								chiduo=A_BuyPosition(); 
								chikong=A_SellPosition();
								If(Abs(chiduo)==0 && CurrentBar > GetGlobalVar(latestoperation) ){
									FileAppend(outputfilename,"perform trade: -- kaikong\t"+Text(slots)+"\t"+Text(restore_price));
									FileAppend(outputtradefilename, Text(CurrentDate)+"-"+Text(CurrentTime)+"\t-- kaikong\t"+Text(slots)+"\t"+Text(currentAsk));
									If(realbuy==True){
										A_SendOrder(Enum_Sell, Enum_Entry, slots, restore_price);
										SetGlobalVar(latestoperation, CurrentBar);
									}
								}Else If(Abs(chiduo)>0 && CurrentBar > GetGlobalVar(latestoperation) ){
									FileAppend(outputfilename,"perform trade: -- pingduo\t"+Text(slots)+"\t"+Text(restore_price));
									FileAppend(outputtradefilename, Text(CurrentDate)+"-"+Text(CurrentTime)+"\t-- pingduo\t"+Text(slots)+"\t"+Text(currentAsk));
									If(realbuy==True){
										If(A_TodayBuyPosition==0){
											A_SendOrder(Enum_Sell, Enum_Exit,slots, restore_price);
										}Else{
											A_SendOrder(Enum_Sell, Enum_ExitToday,slots, restore_price);
										}
										SetGlobalVar(latestoperation, CurrentBar);
									}
								}Else{
									outer_flag = True;
								}
							}
						}
					}
				}	
				
				If( outer_flag == True ){
					outer_count = outer_count+1;
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", restore_type);
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(restore_price)); 
				}
			}
			
			SetTBProfileString(totalsign,"total", Text(outer_count));
			
			
		}

	
	}
	
	
	
	///////////////////////////////
	// Normal Trading
	///////////////////////////////
	
	if(BarStatus==2 && AccountDataExist){
			
			
			NumericArrayClear(openaccountbuy);
			NumericArrayClear(openaccountsell);
			opencount = A_GetOpenOrderCount();

			For i = 1 To opencount{
				
				nBorS = A_OpenOrderBuyOrSell(i);
				nEorE = A_OpenOrderEntryOrExit(i);
				orderprice = A_OpenOrderPrice(i);
				If (nBorS == Enum_Buy() && nEorE==Enum_Entry()){
					
					NumericArrayInsert(openaccountbuy, -1, orderprice);
					
				}Else If(nBorS==Enum_Sell()&& nEorE==Enum_Entry() ){
					
					NumericArrayInsert(openaccountsell, -1, orderprice);
					
				}Else If(nBorS==Enum_Buy()&& ( nEorE==Enum_Exit() || nEorE==Enum_ExitToday() ) ){
					
					NumericArrayInsert(openaccountbuy, -1, orderprice);
					
				}Else If(nBorS==Enum_Sell()&& ( nEorE==Enum_Exit() || nEorE==Enum_ExitToday() ) ){
					
					NumericArrayInsert(openaccountsell, -1, orderprice);
					
				}
			}
			
			outer_count = Value(GetTBProfileString(totalsign,"total"));
			For i = 1 To outer_count{
				restore_type = GetTBProfileString(pinzhong + "_" + Text(i),"type");
				restore_price = Value(GetTBProfileString(pinzhong + "_" + Text(i),"price"));
			
				If(Exact(restore_type,"b")==True){
					NumericArrayInsert(openaccountbuy, -1, restore_price);
					
					
				}
				If(Exact(restore_type,"s")==True){
					NumericArrayInsert(openaccountsell, -1, restore_price);
					
					
				}
			}
			
			chiduo=A_BuyPosition(); 
			chikong=A_SellPosition();
			duokongminus = (chiduo-chikong)/slots - bias_value*2;
			if(realbuy == False){
				duokongminus = GetNumericArraySize(openaccountsell) - GetNumericArraySize(openaccountbuy) - bias_value;
			}
			duokongminus_param = 0; //Floor(Abs(duokongminus)/2,1);
			
			NumericArraySort(openaccountbuy, False);
			NumericArraySort(openaccountsell, True);
			//skw_formal_print_array(outputfilename, "sell forbid", openaccountsell);
			//skw_formal_print_array(outputfilename, "buy forbid", openaccountbuy);
			
			
			//step_value = Value(GetTBProfileString(step_sign,"step"));
			//step_value = Round(step_value/step_base, 0)*step_base;
			
			//step = Max(step_base, step_value) ;
			
			 
			currentAsk = Q_AskPrice();
			currentBid = Q_BidPrice();

			
			If(duokongminus>=1){
				sellprice = Floor(currentBid/step, 1)*step;
				buyprice = sellprice-step*(duokongminus_param+1);
				
			}Else If(duokongminus<=-1){
				buyprice = Ceiling(currentAsk/step, 1)*step;
				sellprice = buyprice+step*(duokongminus_param+1);
				
			}Else{
				sellprice = Ceiling(currentAsk/step, 1)*step;
				buyprice = Floor(currentBid/step, 1)*step;
				
				if (sellprice == buyprice){
					rand_num = Rand(0,10);
					If ( rand_num>=5){
						buyprice = buyprice - step;
					}Else{
						sellprice = sellprice + step;
					}
				}
				
			}
			

			priceinsell = (skw_formal_array_functions("contains", openaccountsell, sellprice) == 1);
			priceinbuy = (skw_formal_array_functions("contains", openaccountbuy, buyprice) == 1);
			
			
			FileAppend(outputfilename,Text(currentAsk)+ "<--->" +Text(currentBid));
			
			
			
			if(sellprice <= buyprice){
				return;
			}
			
			conflict_str = "";
			If(priceinbuy==True){
				conflict_str = conflict_str + "(" + Text(buyprice) + ")";	
			}Else{
				conflict_str = conflict_str + Text(buyprice);	
			}
			conflict_str = conflict_str + "<--buy   sell-->";
			If(priceinsell==True){
				conflict_str = conflict_str + "(" + Text(sellprice) + ")";	
			}Else{
				conflict_str = conflict_str + Text(sellprice);	
			}
			
			
			FileAppend(outputfilename,conflict_str);
			If(priceinsell || priceinbuy){
				return;
			}
			
			today_lowerlimit = Q_LowerLimit();
			today_upperlimit = Q_UpperLimit(); 
			
			
			
		
			If( buyprice>0 && sellprice>buyprice && CurrentBar > GetGlobalVar(latestoperation) ){
					FileAppend(outputfilename,"prepare new trade: " + Text(buyprice) + "  "+ Text(sellprice));
					
					outer_count = Value(GetTBProfileString(totalsign,"total"));
					
					outer_count = outer_count+1;
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "b");
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(buyprice)); 
					
					outer_count = outer_count+1;
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "s");
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(sellprice)); 
					
					SetTBProfileString(totalsign,"total", Text(outer_count));
					
					SetGlobalVar(latestoperation, CurrentBar);
			}
				
			
	}
	 
	
End

//------------------------------------------------------------------------
// 编译版本	GS2015.12.25
// 用户版本	2016/08/01 11:33:20
// 版权所有	straightup
// 更改声明	TradeBlazer Software保留对TradeBlazer平台
//			每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------