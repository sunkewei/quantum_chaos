/*  **************************************
 * Nuclear Version 8.0
 @ Dependency
 skw_formal_array2text
 skw_formal_numerinarray
 
 ******************************************/

Params
	Numeric slots(1);
    Numeric step(3);
	Bool isnight(False);
	
	
Vars
	Numeric globalstage(4);
	Numeric latestkaiduo(5);
	Numeric latestkaikong(6);
	Numeric latestpingduo(7);
	Numeric latestpingkong(8);
	Numeric kepingposi(9);
	
	Numeric kaiduo;
	Numeric kaikong;
	Numeric chiduo;
	Numeric chikong;
	Numeric pingduo;
	Numeric pingkong;
	
	Numeric keping;
	Numeric remain;
	
	Numeric currentAsk;
	Numeric currentBid;
	
	Numeric buyprice; 
	Numeric sellprice;
	
	
	Numeric opencount;
	Numeric totalposition(0);
	Numeric i;
	Numeric nBorS;
	Numeric nEorE;
	
	Numeric stage;
	
	
	Numeric orderprice;
	Numeric orderquantity;
	String ordertype;
	
	NumericArray openaccountbuy[100];
	NumericArray openaccountsell[100];
	
	Bool priceinbuy;
	Bool priceinsell;
	
	
	String filestringbuy; 
	String filestringsell; 
	String outputfilename;
	
	
	
	
	Numeric duokongminus(0);
	Numeric duokongminus_param(0);

	
	Numeric timeholder;
	String pinzhong;
	
	Numeric totalnumber;
	Numeric totalindex;
	String restore_type;
	Numeric restore_quantity;
	Numeric restore_price;
	String totalsign;
	Bool realbuy;
	
	Numeric restore_starttime;
	Numeric restore_endtime;
	
	Numeric today_lowerlimit;
	Numeric	today_upperlimit;
	Numeric outer_count;
	Bool outer_flag;
	
	Numeric this_loop_sell_limit;
	Numeric this_loop_buy_limit;
	
	
	  
Begin
	
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	// Initialization 
	///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	////////////////////////////////
	// Initialize Variables
	////////////////////////////////
	realbuy = True;
	pinzhong = Symbol();
	outputfilename = "c:\\continue_"+pinzhong+".txt";
	
	totalsign = pinzhong+"_total";
	restore_starttime = 0;
	restore_endtime = 0;
	If( barstatus==0 ){
		SetGlobalVar( latestkaiduo, 0 );
		SetGlobalVar( latestkaikong, 0 );
		SetGlobalVar( latestpingduo, 0 );
		SetGlobalVar( latestpingkong, 0 );
		SetGlobalVar( globalstage, -1 );
		SetGlobalVar( kepingposi, 0 );
	}
	
	////////////////////////////////
	// Handle Trade Time
	////////////////////////////////
	
	
	timeholder = CurrentTime();
	FileAppend(outputfilename,"======================"+Text(CurrentTime)+"=============================");
	If (Weekday==6 || Weekday==0){
		FileAppend(outputfilename,"Weekend, no trading");
	}	
	
	If(isnight==True){
		restore_starttime = 0.205500;
		restore_endtime = 0.210000; // only need to modify this one for missing init trading tip (night trade)
		If(timeholder>=0.150010 && timeholder<=0.205500){
			return;
		}
		If(timeholder>=0.000000 && timeholder<=0.085500){
			return;
		}

	}
	
	If(isnight==False){
		restore_starttime = 0.085500;
		restore_endtime = 0.090000;// only need to modify this one for missing init trading tip (day trade)
		If(timeholder>=0.150010 && timeholder<=0.235959){
			return;
		}
		If(timeholder>=0.000000 && timeholder<=0.085500){
			return;
		}
	}
	
	
	
	
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	// Trading
	///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	///////////////////////////////////
	// Judge whether Account is ready
	///////////////////////////////////
	if(BarStatus==2 && AccountDataExist){
		
		today_lowerlimit = Q_LowerLimit();
		today_upperlimit = Q_UpperLimit(); 
		currentAsk = Q_AskPrice();
		currentBid = Q_BidPrice();
		
		If(!(today_lowerlimit > 0 && today_upperlimit > 0 && currentAsk > 0 && currentBid > 0 ) ){
			FileAppend(outputfilename,"----------account not on----------");
			return;
		}
	}
	
	///////////////////////////////
	// End of the Day 
	///////////////////////////////
	
	if(BarStatus==2 && AccountDataExist){
		If((timeholder>=0.145950 && timeholder<=0.151000)){
			If(Value(GetTBProfileString(totalsign,"stored")) == 1){
				FileAppend(outputfilename,"----------already stored----------");
			}
			FileAppend(outputfilename,"----------Entering end of the day----------");
			opencount = A_GetOpenOrderCount();
			outer_count = Value(GetTBProfileString(totalsign,"total"));
			FileAppend(outputfilename,"total Number: endofday1    "+GetTBProfileString(totalsign,"total"));
			SetTBProfileString(totalsign,"total", Text(opencount+outer_count));
			FileAppend(outputfilename,"total Number: endofday2    "+GetTBProfileString(totalsign,"total"));
			
			
			For i = outer_count+1 To outer_count+opencount
			{
				orderquantity = 0;
				nBorS = A_OpenOrderBuyOrSell(i-outer_count);
				nEorE = A_OpenOrderEntryOrExit(i-outer_count);
				orderprice = A_OpenOrderPrice(i-outer_count);
				orderquantity = A_OpenOrderLot(i-outer_count);
				ordertype = "";
				If (nBorS == Enum_Buy() && nEorE==Enum_Entry()){
					ordertype = "kd";
				}Else If(nBorS==Enum_Sell()&& nEorE==Enum_Entry()){
					ordertype = "kk";
				}Else If(nBorS==Enum_Buy()&& ( nEorE==Enum_Exit() || nEorE==Enum_ExitToday() ) ){
					ordertype = "pk";
				}Else If(nBorS==Enum_Sell()&& ( nEorE==Enum_Exit() || nEorE==Enum_ExitToday() ) ){
					ordertype = "pd";
				}
				SetTBProfileString(pinzhong+"_"+Text(i),"type", ordertype);
				SetTBProfileString(pinzhong+"_"+Text(i),"quantity", Text(orderquantity));
				SetTBProfileString(pinzhong+"_"+Text(i),"price", Text(orderprice));
			}
			SetTBProfileString(totalsign,"stored", Text(1));
			return;
		}Else{
			SetTBProfileString(totalsign,"stored", Text(0));
		}
	
	}
	
	
	/////////////////////////////////////////////
	// Before Trading: pre-setting protecting deals
	////////////////////////////////////////////
	if(BarStatus==2 && AccountDataExist){
		
		this_loop_buy_limit = Max(currentBid - 2*step, today_lowerlimit);
		this_loop_sell_limit = Min(currentBid + 2*step, today_upperlimit);
		
		totalnumber = Value(GetTBProfileString(totalsign,"total"));
		FileAppend(outputfilename,"total Number: pre-setting1    "+GetTBProfileString(totalsign,"total"));
		FileAppend(outputfilename,"this loop_buy_limit    "+ Text(this_loop_buy_limit));
		FileAppend(outputfilename,"this loop_sell_limit    "+ Text(this_loop_sell_limit));
		If(totalnumber>0){
			outer_count = 0;
			for  totalindex = 1 To totalnumber {
				outer_flag = False;
				restore_type = GetTBProfileString(pinzhong + "_" + Text(totalindex),"type");
				restore_price = Value(GetTBProfileString(pinzhong + "_" + Text(totalindex),"price"));
				restore_quantity = Value(GetTBProfileString(pinzhong + "_" + Text(totalindex),"quantity"));
				
				If(Exact(restore_type,"kd")==True){
					If( restore_price <= this_loop_buy_limit ){
						outer_flag = True;
					}Else{
						FileAppend(outputfilename,"kaiduo\t"+Text(restore_quantity)+"\t"+Text(restore_price));
						If(realbuy==True){
							A_SendOrder(Enum_Buy,Enum_Entry,restore_quantity,restore_price);
						}
					}
				}
				If(Exact(restore_type,"pk")==True){
					If( restore_price <= this_loop_buy_limit ) {
						outer_flag = True;
					}Else{
						FileAppend(outputfilename,"pingkong\t"+Text(restore_quantity)+"\t"+Text(restore_price));
						If(realbuy==True){
							A_SendOrder(Enum_Buy,Enum_Exit,restore_quantity, restore_price);
						}
					}
				}
				
				If(Exact(restore_type,"kk")==True){
					If( restore_price >= this_loop_sell_limit ){
						outer_flag = True;
					}Else{
						FileAppend(outputfilename,"kaikong\t"+Text(restore_quantity)+"\t"+Text(restore_price));
						If(realbuy==True){
							A_SendOrder(Enum_Sell, Enum_Entry, restore_quantity, restore_price);
						}
					}
				}
				
				If(Exact(restore_type,"pd")==True){
					If( restore_price >= this_loop_sell_limit ){
						outer_flag = True;
					}Else{
						FileAppend(outputfilename,"pingduo\t"+Text(restore_quantity)+"\t"+Text(restore_price));
						If(realbuy==True){
							A_SendOrder(Enum_Sell, Enum_Exit,restore_quantity, restore_price);
						}
					}
				}
				
				
				If( outer_flag == True ){
					outer_count = outer_count+1;
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", restore_type);
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"quantity", Text(restore_quantity));
					SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(restore_price)); 
				}
			}
			
			SetTBProfileString(totalsign,"total", Text(outer_count));
			FileAppend(outputfilename,"total Number: pre-setting2    "+GetTBProfileString(totalsign,"total"));
			FileAppend(outputfilename,"Outer Size: "+Text(outer_count));
			
		}

	
	}
	
	
	If(timeholder>=restore_starttime && timeholder<=restore_endtime){
		FileAppend(outputfilename,"----------Restore time, no trading----------");
		return;
	}
	
	
	///////////////////////////////
	// Normal Trading
	///////////////////////////////
	
	if(BarStatus==2 && AccountDataExist){
			FileAppend(outputfilename,"----------- Enter Normal Trading -----------");
			kaiduo=0;
			kaikong=0;
			pingduo=0;
			pingkong=0;
			chiduo=A_BuyPosition(); 
			chikong=A_SellPosition();
			NumericArrayClear(openaccountbuy);
			NumericArrayClear(openaccountsell);
			opencount = A_GetOpenOrderCount();

			For i = 1 To opencount{
				remain =0;
				nBorS = A_OpenOrderBuyOrSell(i);
				nEorE = A_OpenOrderEntryOrExit(i);
				orderprice = A_OpenOrderPrice(i);
				remain = A_OpenOrderLot(i);
				If (nBorS == Enum_Buy() && nEorE==Enum_Entry()){
					kaiduo = kaiduo+remain;
					NumericArrayInsert(openaccountbuy, 0, orderprice);
					
				}Else If(nBorS==Enum_Sell()&& nEorE==Enum_Entry() ){
					kaikong = kaikong+remain;
					NumericArrayInsert(openaccountsell, 0, orderprice);
					
				}Else If(nBorS==Enum_Buy()&& ( nEorE==Enum_Exit() || nEorE==Enum_ExitToday() ) ){
					pingkong = pingkong+remain;
					NumericArrayInsert(openaccountbuy, 0, orderprice);
					
				}Else If(nBorS==Enum_Sell()&& ( nEorE==Enum_Exit() || nEorE==Enum_ExitToday() ) ){
					pingduo = pingduo+remain;
					NumericArrayInsert(openaccountsell, 0, orderprice);
					
				}
			}
			
			outer_count = Value(GetTBProfileString(totalsign,"total"));
			FileAppend(outputfilename,"total Number: normaltrade1   "+GetTBProfileString(totalsign,"total"));
			For i = 1 To outer_count{
				restore_type = GetTBProfileString(pinzhong + "_" + Text(i),"type");
				restore_price = Value(GetTBProfileString(pinzhong + "_" + Text(i),"price"));
				restore_quantity = Value(GetTBProfileString(pinzhong + "_" + Text(i),"quantity"));
			
				If(Exact(restore_type,"kd")==True){
					kaiduo = kaiduo+restore_quantity;
					NumericArrayInsert(openaccountbuy, 0, restore_price);
					
					
				}Else If(Exact(restore_type,"kk")==True){
					kaikong = kaikong+restore_quantity;
					NumericArrayInsert(openaccountsell, 0, restore_price);
					
					
				}Else If(Exact(restore_type,"pk")==True){
					pingkong = pingkong+restore_quantity;
					NumericArrayInsert(openaccountbuy, 0, restore_price);
					
					
				}Else If(Exact(restore_type,"pd")==True){
					pingduo = pingduo+restore_quantity;
					NumericArrayInsert(openaccountsell, 0, restore_price);
					
				}
			}
	 
			duokongminus = chiduo-chikong;
			duokongminus_param = Floor(Abs(duokongminus/slots)/2,1);
			if (duokongminus_param < 0){
				duokongminus_param = 0;
			}
			
			filestringbuy = "buy forbid: "+skw_formal_array2text(openaccountbuy);
			filestringsell = "sell forbid: "+skw_formal_array2text(openaccountsell);
			FileAppend(outputfilename,filestringbuy);
			FileAppend(outputfilename,filestringsell);
			
			
			SetGlobalVar(globalstage, 0);
			stage = 0;
			
			If(Min((chiduo-pingduo),(chikong-pingkong))>=1){ 
				SetGlobalVar(globalstage, 1);
				stage = 1;
			}
			
			FileAppend(outputfilename,"I am on Stage: "+ Text(stage));
			
			
			currentAsk = Q_AskPrice();
			currentBid = Q_BidPrice();

			
			If((duokongminus/slots)>0){
				sellprice = Floor(currentBid/step, 1)*step;
				buyprice = sellprice-step*(duokongminus_param+1);
			} 
			If((duokongminus/slots)<0){
				buyprice = Ceiling(currentAsk/step, 1)*step;
				sellprice = buyprice+step*(duokongminus_param+1);
			}
			If((duokongminus/slots)==0){
				sellprice = Ceiling(currentAsk/step, 1)*step;
				buyprice = Floor(currentBid/step, 1)*step;
			}
			

			priceinsell = skw_formal_numerinarray(openaccountsell, sellprice, step, -1, 1);
			priceinbuy = skw_formal_numerinarray(openaccountbuy, buyprice,step, -1, 1);
			
			FileAppend(outputfilename,"proposed sell "+ Text(sellprice));
			FileAppend(outputfilename,"proposed buy "+ Text(buyprice));
			if(sellprice <= buyprice){
				return;
			}
			If( (sellprice*1000)%(step*1000)!=0
				|| (buyprice*1000)%(step*1000)!=0){
				FileAppend(outputfilename,"current sell price not on edge");
				return;
			}
			
			If(priceinsell==True){
				FileAppend(outputfilename,"current sell price "+Text(sellprice)+" conflict, can not sell");
				return;
			}
			If(priceinbuy==True){
				FileAppend(outputfilename,"current buy price "+Text(buyprice)+" conflict, can not buy");
				return;
			}
			
			today_lowerlimit = Q_LowerLimit();
			today_upperlimit = Q_UpperLimit(); 
			
			
			If(stage==0 && GetGlobalVar(globalstage)==0){		
		
				If( buyprice>0  
					&& sellprice>buyprice
					&& CurrentBar > GetGlobalVar(latestkaiduo)
					&& CurrentBar > GetGlobalVar(latestkaikong)
					&& CurrentBar > GetGlobalVar(latestpingduo)
					&& CurrentBar > GetGlobalVar(latestpingkong)
				  ){
						If(GetGlobalVar(latestkaiduo)==0||GetGlobalVar(latestkaikong)==0){
							SetGlobalVar(latestkaiduo, CurrentBar);
							SetGlobalVar(latestkaikong, CurrentBar);
							return;
						}
						
						outer_count = Value(GetTBProfileString(totalsign,"total"));
						FileAppend(outputfilename,"total Number: normaltrade2    "+GetTBProfileString(totalsign,"total"));
						
						If(buyprice<today_lowerlimit || buyprice>today_upperlimit){
							outer_count = outer_count+1;
							SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "kd");
							SetTBProfileString(pinzhong + "_" + Text(outer_count),"quantity", Text(slots));
							SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(buyprice)); 
							
							SetTBProfileString(totalsign,"total", Text(outer_count));
							FileAppend(outputfilename,"total Number: normaltrade3    "+GetTBProfileString(totalsign,"total"));
							
						}Else{
							If(realbuy==True){
								A_SendOrder(Enum_Buy,Enum_Entry,slots,buyprice);
							}
						}
						SetGlobalVar(latestkaiduo, CurrentBar);
						
						If(sellprice<today_lowerlimit || sellprice>today_upperlimit){
							outer_count = outer_count+1;
							SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "kk");
							SetTBProfileString(pinzhong + "_" + Text(outer_count),"quantity", Text(slots));
							SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(sellprice)); 
							SetTBProfileString(totalsign,"total", Text(outer_count));
							FileAppend(outputfilename,"total Number: normaltrade4    "+GetTBProfileString(totalsign,"total"));
						}Else{
							If(realbuy==True){
								A_SendOrder(Enum_Sell, Enum_Entry, slots, sellprice);
							}
						}
						SetGlobalVar(latestkaikong, CurrentBar);
				}
				
			}
			If(stage==1 && GetGlobalVar(globalstage)==1){
				keping = 0;
				keping = Min(chiduo-pingduo, chikong-pingkong);
				If(keping<=0){
					return;
				}
				
				If(keping>0 
					&& sellprice>buyprice 
					&& buyprice>0
					&& CurrentBar > GetGlobalVar(latestkaiduo)
					&& CurrentBar > GetGlobalVar(latestkaikong)
					&& CurrentBar > GetGlobalVar(latestpingduo)
					&& CurrentBar > GetGlobalVar(latestpingkong)
					){
						If(GetGlobalVar(kepingposi)==keping){
							If(sellprice<today_lowerlimit || sellprice>today_upperlimit){
								outer_count = outer_count+1;
								SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "pd");
								SetTBProfileString(pinzhong + "_" + Text(outer_count),"quantity", Text(slots));
								SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(sellprice)); 
								SetTBProfileString(totalsign,"total", Text(outer_count));
								FileAppend(outputfilename,"total Number: normaltrade5    "+GetTBProfileString(totalsign,"total"));
								
							}Else{
								If(realbuy==True){
									A_SendOrder(Enum_Sell, Enum_Exit,Min(keping,slots), sellprice);
								}
							}
							SetGlobalVar(latestpingduo, CurrentBar);
							If(buyprice<today_lowerlimit || buyprice>today_upperlimit){
								outer_count = outer_count+1;
								SetTBProfileString(pinzhong + "_" + Text(outer_count),"type", "pk");
								SetTBProfileString(pinzhong + "_" + Text(outer_count),"quantity", Text(slots));
								SetTBProfileString(pinzhong + "_" + Text(outer_count),"price", Text(buyprice)); 
								SetTBProfileString(totalsign,"total", Text(outer_count));
								FileAppend(outputfilename,"total Number: normaltrade6    "+GetTBProfileString(totalsign,"total"));
							}Else{
								If(realbuy==True){
									A_SendOrder(Enum_Buy,Enum_Exit,Min(keping,slots), buyprice);
								}
							}
							SetGlobalVar(latestpingkong, CurrentBar);
							SetGlobalVar(kepingposi, 0);
						}Else{
							SetGlobalVar(kepingposi, keping);
						}
					
				}
			}
	
	}
	 
	
End

//------------------------------------------------------------------------
// 编译版本	GS2015.12.25
// 用户版本	2016/08/01 11:33:20
// 版权所有	straightup
// 更改声明	TradeBlazer Software保留对TradeBlazer平台
//			每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------